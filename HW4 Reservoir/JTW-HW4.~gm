$ontext
CEE 5410 - Water Resources Systems Analysis
Reservoir Optimazation problem for HW4

THE PROBLEM:

A reservoir must maintain 5 units of water of storage at the end
of an irrigation period. A water manager must decide how much to release through
the turbines, how much to retain in the reservoir, and how much to irrigate a
large area of farmland while maintaining a flow of 1 unit in the river at the
end of the system. Data are as follows:

Month    Inflow Units    Hydro Benefits ($/unit) Irr. Benefits ($/Unit)
1        2               1.6                     1.0
2        2               1.7                     1.2
3        3               1.8                     1.9
4        4               1.9                     2.0
5        5               2.0                     2.2
5        6               2.0                     2.2

The optimization depends on both the storage location and time (month scale)
Decision Variables
VRes(t) - Storage volume of the reservoir (Units), for all t
VSpill - Volume released via the spillway (Units), for all t
VTurb - Volume released via the hydropower turbines (Units), for all t
VIrr - Volume diverted to irrigated farmland (Units), for all t
VRiver - Volume over the course of each month left in the river (Units), for all t

Objective Function
Maximize benefits derived from hydropower and irrigation


THE SOLUTION:
Uses General Algebraic Modeling System to Solve this Linear Program

Joshua T Ward
joshua.timothy.ward@aggiemail.usu.edu / joshua.ward@usu.edu

September 25, 2020
$offtext


*Define the sets containing decision variables and resource constraints
SETS L Water allocation locations in reservoir system
         /res "Reservoir", sp "Spillway", hyd "Hydropower", irr "Irrigation", riv "River"/
     t Month of period of interest
         /M1, M2, M3, M4, M5, M6/;

*Define the parameters and coefficients
PARAMETERS
         I(t) Inflow values in each month t (units of water)
         /M1 2, M2 2, M3 3, M4 4, M5 3, M6 2/;


TABLE c(L, t) Objective function coefficients at location L in month t ($ per unit)
         M1      M2      M3      M4      M5      M6
hyd      1.6     1.7     1.8     1.9     2.0     2.0
irr      1.0     1.2     1.9     2.0     2.2     2.2 ;

VARIABLES X(L, t) water allocations by site and month (units of water)
          VPROFIT total profit ($);

* Define the bounds of the constraint inequalities
SCALAR
         BEG_STO Beginning reservoir storage at start of model period (units of water) / 5 /
         RES_MAX Maximum reservoir capacity (units of water) / 9 /
         TUR_MAX Maximum flow through the turbines (units of water) / 4 /
         RIV_MIN Minimum flow required in the river after diversion point B (units of water) / 1 /;

*Define non-negativity constraints on the variables
POSITIVE VARIABLES X;

$ontext


$offtext
EQUATIONS
         PROFIT Total profit ($) and objective function value

         IRES_MASSBAL Initial reservoir mass balance (units of water)
         RESERVE_MASSBAL(t) Reservoir mass balance (units of water)
         DIVERT_MASSBAL(t) Mass balance at diversion point B (units of water)
         FLOW_RIVER(t) Minimum flow requirement in river (units of water)
         RESERVE_STORAGE(t) Reservoir net change in storage (units of water)
         TURBINE_CAP(t) Hydropower capacity of reservoir turbines (units of water)
         RESERVE_CAP(t) Reservoir capacity (units of water);

* Build the constraint equations

IRES_MASSBAL..                           BEG_STO+I("M1")-X("sp","M1")-X("hyd","M1") =E= X("res","M1");
RESERVE_MASSBAL(t)$(ORD(t) ge 2 )..      I(t)+X("res",t-1)-X("sp",t)-X("hyd", t) =E= X("res",t);
DIVERT_MASSBAL(t) ..                     X("hyd",t)+X("sp",t)-X("irr",t)-X("riv",t) =E= 0;
FLOW_RIVER(t) ..                         X("riv",t) =G= RIV_MIN;
RESERVE_STORAGE(t) ..                    X("res","M6") =G= BEG_STO;
TURBINE_CAP(t) ..                        X("hyd", t) =L= TUR_MAX;
RESERVE_CAP(t) ..                        X("res",t) =L= RES_MAX;

* Build the objective function
PROFIT..                     VPROFIT =E= SUM(t, c("hyd",t)*X("hyd",t)+c("irr",t)*X("irr",t)) ;

* Include all equations in the model
MODEL RES /ALL/;

* Solve the model
SOLVE RES USING LP MAXIMIZING VPROFIT;

DISPLAY rhsrng FLOW_RIVER;

* Save a gdx file for easier viewing of the tables
EXECUTE_UNLOAD "JTW_HW4.gdx";




